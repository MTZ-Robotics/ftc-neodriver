plugins {
    id 'com.android.library'
    id "maven-publish"
    id "signing"
}

android {
    namespace 'com.w8wjb.ftc'
    compileSdk 29

    defaultConfig {
        minSdk 24
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.annotation:annotation:1.7.0'

    implementation 'org.firstinspires.ftc:Inspection:9.0.1'
    implementation 'org.firstinspires.ftc:Blocks:9.0.1'
    implementation 'org.firstinspires.ftc:Tfod:9.0.1'
    implementation 'org.firstinspires.ftc:RobotCore:9.0.1'
    implementation 'org.firstinspires.ftc:RobotServer:9.0.1'
    implementation 'org.firstinspires.ftc:OnBotJava:9.0.1'
    implementation 'org.firstinspires.ftc:Hardware:9.0.1'
    implementation 'org.firstinspires.ftc:FtcCommon:9.0.1'
    implementation 'org.firstinspires.ftc:Vision:9.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

tasks.register('androidJavadocs', Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

tasks.register('androidJavadocsJar', Jar) {
    dependsOn androidJavadocs
    archiveClassifier = "javadoc"
    from androidJavadocs.destinationDir
}

project.afterEvaluate {
    publishing {
        publications {
            ftcLibrary(MavenPublication) {

                artifact bundleReleaseAar
                artifact androidSourcesJar
                artifact androidJavadocsJar

                groupId = "com.w8wjb.ftc"
                artifactId = "ftc-neodriver"
                version = '1.0-SNAPSHOT'

                pom {
                    description = "Hardware driver for the Adafruit NeoDriver for use with the FIRST Tech Challenge Control Hub"
                    name = "FTC NeoDriver"
                    url = "https://github.com/w8wjb/ftc-neodriver"

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://opensource.org/licenses/MIT"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "w8wjb"
                            name = "Wes Bustraan"
                            email = "wbustraan@gmail.com"
                        }
                    }

                    scm {
                        url = "https://github.com/w8wjb/ftc-neodriver"
                    }
                }

            }
        }
    }
}

